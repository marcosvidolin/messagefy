buildscript {
    ext.projectName = 'messagefy'
    ext.projectGroup = 'com.vidolima'
    ext.projectArtifact = 'messagefy'
    ext.projectVersion = 'v1.1.1'

    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}
group projectGroup
version projectVersion

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        publish(MavenPublication) {
            from components.java
            groupId projectGroup
            artifactId projectArtifact
            version projectVersion
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = projectName
        desc = 'This is a very small helper java library to easily build a JavaMail MIME object (javax.mail.internet.MimeMessage).'
        websiteUrl = 'https://github.com/marcosvidolin/messagefy'
        issueTrackerUrl = 'https://github.com/marcosvidolin/messagefy/issues'
        vcsUrl = 'https://github.com/marcosvidolin/messagefy.git'
        licenses = ['MIT']
        labels = ['java', 'JavaMail', 'MimeMessage', 'Library', 'Message']
        version {
            name = projectVersion
            released  = new Date()
            vcsTag = projectVersion
            gpg {
                sign = true
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile 'javax.mail:javax.mail-api:1.5.5'
    compile 'org.slf4j:slf4j-api:1.7.12'
    testCompile 'junit:junit:4.12'
}